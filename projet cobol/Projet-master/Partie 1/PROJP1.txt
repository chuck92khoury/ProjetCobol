000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PROJP1.
000300 ENVIRONMENT DIVISION.
000400 CONFIGURATION SECTION.
000500 SPECIAL-NAMES.
000600     DECIMAL-POINT IS COMMA.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900      SELECT NEWPRDS ASSIGN TO NEWPRODS
001000           ORGANIZATION IS SEQUENTIAL.
001010      SELECT TAUXCHS ASSIGN TO TAUXCHNG
001011           ORGANIZATION IS INDEXED
001012           ACCESS MODE IS SEQUENTIAL
001013           RECORD KEY IS TAUXCHS-TYPE
001014           FILE STATUS IS FS-TAUX.
001100 DATA DIVISION.
001200 FILE SECTION.
001300 FD NEWPRDS.
001400  01 NEWPRODS-RECORD PIC X(45).
001500 FD TAUXCHS.
001600  01 TAUXCHS-RECORD.
001700   05 TAUXCHS-TYPE   PIC X(2).
001800   05 TAUXCHS-TAUX   PIC 9V9(10).
001900   05 FILLER         PIC X(2).
002210
002300 WORKING-STORAGE SECTION.
002301     EXEC SQL
002302         INCLUDE SQLCA
002303     END-EXEC
002304     EXEC SQL
002305         INCLUDE PRODUCT
002306     END-EXEC
002307
002310 01 DELIMITER-VAR       PIC X VALUE ';'.
002400 01 WS-DATA.
002410  05 FS-TAUX            PIC X(2).
002500  05 WS-PRO-ID          PIC X(3).
002600  05 WS-DESCRIPTION     PIC X(20).
002610  05 WS-DESC-FORM       PIC X(20).
002620  05 WS-I               PIC 9(2) VALUE 1.
002630  05 LETTRE-ACT         PIC X.
002640  05 MAJ                PIC X.
002650  05 MIN                PIC X.
002660  05 FIRST-WORD         PIC X VALUE 'O'.
002670  05 POINT-OUT          PIC 9(2) VALUE 1.
002700  05 WS-PRICE           PIC 9(3)v99.
002701  05 WS-PRICE-TEXT      PIC X(10).
002800  05 WS-DEVISE          PIC X(2).
002900  05 WS-PRICE-USD       PIC 9(3)V99 COMP-3.
002910  05 ED-PRICE-USD       PIC ZZ9,99.
003000  05 WS-CONVERSION-RATE PIC 9V9999.
003010 01 WS-ANO              PIC 99 VALUE 12.
003020 01 WS-VAR              PIC 9 VALUE 0.
003100 01 WS-FLAG-END         PIC 9.
003200  88 END-OF-FILE-NEWPRDS  VALUE 2.
003202 01 TAB-DEVISE.
003203  05 TAB-DEV OCCURS 5.
003204   10 DEV-TYPE PIC X(2).
003205   10 DEV-TAUX PIC 9V9(10).
003210
003300 PROCEDURE DIVISION.
003310
003500     PERFORM OPEN-FILES
003510*    PERFORM ALIMENTATION-TABLEAU
003600     PERFORM TRAITEMENT-FICHIER UNTIL END-OF-FILE-NEWPRDS
003610     PERFORM COMMIT-SQL
003700     PERFORM CLOSE-FILES
003800     STOP RUN.
003810*----------------------------------------------------------------
003900 OPEN-FILES.
003910*----------------------------------------------------------------
004000     OPEN INPUT NEWPRDS
004010     OPEN INPUT TAUXCHS
004100     .
004110*----------------------------------------------------------------
004200 CLOSE-FILES.
004210*----------------------------------------------------------------
004300     CLOSE NEWPRDS
004310     CLOSE TAUXCHS
004400     .
004401*----------------------------------------------------------------
004402 READ-NEWPRDS.
004403*----------------------------------------------------------------
004404     READ NEWPRDS AT END
004405      SET END-OF-FILE-NEWPRDS TO TRUE
004406     END-READ
004407     .
004430*----------------------------------------------------------------
004500 TRAITEMENT-FICHIER.
004501* LECTURE DU FICHIER ET TRAITEMENT DE LA LIGNE EN COURS
004510*----------------------------------------------------------------
004520     PERFORM READ-NEWPRDS
005010     PERFORM TRAITEMENT-LIGNE
005020     .
005030*----------------------------------------------------------------
005100 TRAITEMENT-LIGNE.
005101*TRAITEMENT DE LA LIGNE QUI VA êTRE DéCOMPOSéE EN PLUSIEURS
005102*VARIABLES POUR êTRE ENSUITE INSéRéE EN BASE DE DONNéE
005110*----------------------------------------------------------------
005200     UNSTRING NEWPRODS-RECORD
005300             DELIMITED BY DELIMITER-VAR
005400             INTO WS-PRO-ID
005500                  WS-DESCRIPTION
005510                  WS-PRICE-TEXT
005511                  WS-DEVISE
005520     END-UNSTRING.
005522     INSPECT WS-PRICE-TEXT CONVERTING '.' TO ','
005600     PERFORM FORMATTAGE-DESCRIPTION
005700     PERFORM CONVERT-TO-USD
005800     PERFORM INSERT-INTO-DB
005810     .
005820*----------------------------------------------------------------
005900 FORMATTAGE-DESCRIPTION.
005901*FORMATTAGE DE LA DESCRIPTION DU PRODUIT POUR AVOIR UNE MAJUSCULE
005902*AU DéBUT DE CHAQUE MOT ET LE RESTE EN MINUSCULE
005910*----------------------------------------------------------------
005911     DISPLAY 'AVANT FORMATAGE : ' WS-DESCRIPTION
005913     PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I >
005920                               FUNCTION LENGTH(WS-DESCRIPTION)
005922        MOVE WS-DESCRIPTION(WS-I:1) TO LETTRE-ACT
005923
005925        IF FIRST-WORD = 'O' OR LETTRE-ACT = ' '
005926           MOVE 'N' TO FIRST-WORD
005928           IF LETTRE-ACT = ' '
005929              MOVE 'O' TO FIRST-WORD
005930              MOVE LETTRE-ACT TO WS-DESC-FORM(POINT-OUT:1)
005931           ELSE
005932              MOVE FUNCTION UPPER-CASE(LETTRE-ACT) TO MAJ
005933              MOVE MAJ TO WS-DESC-FORM(POINT-OUT:1)
005935           END-IF
005936        ELSE
005937           MOVE FUNCTION LOWER-CASE(LETTRE-ACT) TO MIN
005941           MOVE MIN TO WS-DESC-FORM(POINT-OUT:1)
005942        END-IF
005943        ADD 1 TO POINT-OUT
005945     END-PERFORM
005946     DISPLAY 'APRES FORMATAGE : ' WS-DESC-FORM
005948     MOVE 1 TO POINT-OUT
005950     .
006110*----------------------------------------------------------------
006200 CONVERT-TO-USD.
006201*CONVERSION DU PRIX ACTUEL SI LA DEVISE EST DIFFéRENTE DE DOLLAR
006210*----------------------------------------------------------------
006220*    PERFORM UNTIL WS-I > 5 OR WS-DEVISE = DEV-TYPE(WS-I)
006223*      IF WS-DEVISE = DEV-TYPE(WS-I)
006224*         COMPUTE WS-PRICE-USD =
006225*                  FUNCTION NUMVAL(WS-PRICE-TEXT) * DEV-TAUX(WS-I)
006226*      END-IF
006230*    END-PERFORM
006231*    MOVE 1 TO WS-I
006232     MOVE WS-DEVISE TO TAUXCHS-TYPE
006233     START TAUXCHS KEY IS EQUAL TO TAUXCHS-TYPE
006234     EVALUATE FS-TAUX
006235      WHEN ZERO
006236           IF WS-DEVISE = 'DO'
006237              MOVE WS-PRICE TO WS-PRICE-USD
006238           ELSE
006239           READ TAUXCHS
006240           COMPUTE WS-PRICE-USD =
006241                   FUNCTION NUMVAL(WS-PRICE-TEXT) * TAUXCHS-TAUX
006242           END-IF
006243      WHEN 23
006244           DISPLAY 'TAUX NON REPERTORIEE'
006245      WHEN OTHER
006246           DISPLAY 'ERREUR START FILE STATUS : ' FS-TAUX
006247      END-EVALUATE
006930     .
006940*-----------------------------------------------------------------
007000 INSERT-INTO-DB.
007001*UNE REQUETE D'INSERTION EST EFFECTUEE DANS LA BASE DE DONNEE
007003*-----------------------------------------------------------------
007210     MOVE WS-PRICE-USD TO PRO-PRICE
007300     EXEC SQL
007400        INSERT INTO API15.PRODUCTS
007500        VALUES    (:WS-PRO-ID,
007600                   :WS-DESC-FORM,
007700                   :PRO-PRICE)
008100     END-EXEC
008200     INITIALIZE WS-DESC-FORM
008300     PERFORM EVAL-INSERT
008400     .
008500*-----------------------------------------------------------------
008600 EVAL-INSERT.
008610*ON éVALUE LA REQUETE D'INSERTION POUR SAVOIR SI TOUT S'EST
008620*EFFECTUé COMME IL FAUT
008700*-----------------------------------------------------------------
008800     EVALUATE TRUE
008900     WHEN SQLCODE = ZERO
009000          DISPLAY 'INSERT OK'
009100     WHEN SQLCODE = -803
009200          DISPLAY 'ERREUR INSERT DOUBLON : ' ws-pro-id
009300     WHEN SQLCODE > 0
009400          DISPLAY 'WARNING SQL : ' SQLCODE
009500     WHEN OTHER
009600          DISPLAY 'ABEND SQL : ' SQLCODE
009700          DISPLAY SQLSTATE
009800          DISPLAY SQLERRM
009900          DISPLAY SQLERRP
010000          DISPLAY SQLERRD(3)
010100          PERFORM ABEND-PROG
010110     END-EVALUATE
010200     .
010300*-----------------------------------------------------------------
010400  COMMIT-SQL.
010500*-----------------------------------------------------------------
010600     EXEC SQL COMMIT END-EXEC
010700     PERFORM EVAL-SQLCODE
010800     .
010900*-----------------------------------------------------------------
011000 ABEND-PROG.
011010* SI IL Y A EU UN SOUCIS AU NIVEAU DE CERTAINES REQUETES, ON
011020* EFFECTUE UN ROLLBACK POUR ANNULER LES MODIFICATIONS
011100*-----------------------------------------------------------------
011110     EXEC SQL ROLLBACK END-EXEC
011200     COMPUTE WS-ANO = WS-ANO / WS-VAR.
011300*-----------------------------------------------------------------
011400 EVAL-SQLCODE.
011500*-----------------------------------------------------------------
011600     EVALUATE TRUE
011700     WHEN SQLCODE = ZERO
011800          CONTINUE
011900     WHEN SQLCODE > 0
012000          IF SQLCODE = +100 THEN
012100             DISPLAY 'FIN TABLE !'
012200          ELSE
012300             DISPLAY 'WARNING : ' SQLCODE
012400          END-IF
012500     WHEN OTHER
012600          DISPLAY 'ANOMALIE GRAVE : ' SQLCODE
012700     END-EVALUATE
012800     .
